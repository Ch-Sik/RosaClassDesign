tree("Root")
    sequence
        tree("Idle")
        tree("Phase0")
        tree("PhaseChange")
        tree("Phase1")

// 플레이어가 보스방에 들어가기 전 보스 대기 상태
tree("Idle")
  not
    repeat
      not
        isEnemyCloserThan(999.9)  // 플레이어가 인식 범위에 들어오면 Idle 종료

// 1페이즈
tree("Phase0") 
  not                          // Phase 종료 조건 달성했을 때 Fail 대신 Succeed 반환하도록 not 게이트 사용
    repeat 
      sequence
        isHpOverPhaseLimit(0)           // HP 체크하고 만약 HP가 기준 아래(=fail)일 경우 Phase2로 이행
        mute                            // 패턴 하나의 실패로 발생한 fail 때문에 페이즈 바뀌는 거 방지   
            fallback                    // 여기서부터 실제 패턴 등장
                // '바위던지기' 서브트리
                sequence
                    CheckCountGEQ("ThrowRock",3)        // 바위던지기 사용 가능?
                    // 바위던지기 사용
                    ThrowRock()
                    ResetCount("ThrowRock")
                // 이하 평타 패턴
                sequence
                    isEnemyCloserThan(5.0)
                    tree("MeleeAttack")
                    AddCount("ThrowRock")
                sequence
                    CheckCountGEQ("Feather",3)            // 씬에 깃털 갯수 3개 이상?
                    // 깃털 회수 패턴 사용
                    RetrieveFeather()
                    ResetCount("Feather")
                    AddCount("ThrowRock")
                sequence
                    // 깃털 발사 패턴
                    FeatherAttack()
                    AddCount("Feather")
                    AddCount("ThrowRock")

// 페이즈 전환 시
tree("PhaseChange")
    sequence
        RoarAttack()
        ResetCount("ThrowRock")

// 2페이즈
tree("Phase1")
  repeat                                // 마지막 Phase에서는 HP 체크할 필요 없음.
    mute                                // 패턴 하나의 실패로 발생한 fail 때문에 페이즈 바뀌는 거 방지
        fallback                    // 여기서부터 실제 패턴 등장
            // '주작' 서브트리
            sequence
                CheckCooldown("Pheonix", 25.0)            // 주작 사용 가능?
                RisingPheonix()
            // '바위던지기' 서브트리
            sequence
                CheckCountGEQ("ThrowRock",4)            // 바위던지기 사용 가능?
                ThrowRock()
                ResetCount("ThrowRock")
            // 이하 평타 서브트리들
            sequence
                isEnemyCloserThan(5.0)
                tree("MeleeAttack")
                AddCount("ThrowRock")
            sequence
                CheckCountGEQ("Feather",3)            // 씬에 깃털 갯수 3개 이상?
                // 깃털 회수 패턴 사용
                RetrieveFeather()
                ResetCount("Feather")
                AddCount("ThrowRock")
            sequence
                // 아니라면 깃털 발사 패턴 사용
                FeatherAttack()
                AddCount("Feather")
                AddCount("ThrowRock")


// 여기서부터는 각각의 공격패턴만 따로 구현한 서브트리들

tree("MeleeAttack")
    MeleeAttack()

